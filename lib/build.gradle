apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.android.library'

repositories {
    google()
}

publish {
    userOrg = 'farmerbb'
    repoName = 'libtaskbar'
    groupId = 'com.farmerbb'
    artifactId = 'libtaskbar'
    publishVersion = getLibVersion()
    desc = 'libtaskbar allows you to quickly and easily add support for Android 10\'s Desktop Mode to any third-party launcher, powered by the Taskbar app.'
    website = 'https://github.com/farmerbb/Taskbar/blob/master/lib/README.md'
}

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME

        buildConfigField "String", "APPLICATION_ID", "\"com.farmerbb.taskbar\""
        buildConfigField "String", "BASE_APPLICATION_ID", "\"com.farmerbb.taskbar\""
        buildConfigField "String", "PAID_APPLICATION_ID", "\"com.farmerbb.taskbar.paid\""
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.taskbar.support\""
        buildConfigField "String", "ANDROIDX86_APPLICATION_ID", "\"com.farmerbb.taskbar.androidx86\""

        buildConfigField "long", "TIMESTAMP", "${System.currentTimeMillis()}L"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java { srcDirs('../app/src/main/java', '../app/src/lib/java', '../app/src/nonplaystore/java') }
            res { srcDirs('../app/src/main/res', '../app/src/lib/res') }
            manifest.srcFile '../app/src/lib/AndroidManifest.xml'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "androidx.legacy:legacy-support-v4:$SUPPORT_V4_VERSION"
    implementation "androidx.appcompat:appcompat:$APPCOMPAT_VERSION"
    implementation "com.google.android.material:material:$MATERIAL_VERSION"
}

def getLibVersion() {
    def appVersion = VERSION_NAME
    if(appVersion.split("\\.").length == 2)
        appVersion = "$appVersion.0"

    def libVersion = appVersion.split("\\.")
    libVersion[0] = "${libVersion[0].toInteger() - 4}".toString()

    return libVersion.join('.')
}
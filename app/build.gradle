ext {
    SUPPORT_LIBRARY_VERSION = "25.3.0"
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.farmerbb.taskbar"
        minSdkVersion 21
        targetSdkVersion 25

        versionCode 137
        versionName "3.2"

        resConfigs "en", "ja", "ru", "de"

        buildConfigField "String", "BASE_APPLICATION_ID", "\"com.farmerbb.taskbar\""
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.taskbar.support\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [appName: "@string/app_name"]
        }
    }

    productFlavors {
        paid {
            applicationId "com.farmerbb.taskbar.paid"
        }

        free {
            applicationId "com.farmerbb.taskbar"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    compile 'com.github.enricocid:cpl:1.0.2'
    compile 'moe.banana:toast-compat:1.0.5'
}


ext {
    SUPPORT_LIBRARY_VERSION = "27.1.1"
    SDK_VERSION = 28
}

apply plugin: 'com.android.application'

repositories {
    google()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.farmerbb.taskbar"
        minSdkVersion 21

        //noinspection OldTargetApi
        targetSdkVersion 27

        // TODO keep versionName and versionCode in sync with Android-x86 manifest
        versionCode 189
        versionName "4.0"

        resConfigs "en", "ja", "ru", "de", "zh-rCN", "tr"

        buildConfigField "String", "BASE_APPLICATION_ID", "\"com.farmerbb.taskbar\""
        buildConfigField "String", "PAID_APPLICATION_ID", "\"com.farmerbb.taskbar.paid\""
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.taskbar.support\""
        buildConfigField "String", "ANDROIDX86_APPLICATION_ID", "\"com.farmerbb.taskbar.androidx86\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]

            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${project.parent.name}-${defaultConfig.versionName}.apk"
                }
            }
        }
    }

    flavorDimensions "default"

    productFlavors {
        paid { applicationId "com.farmerbb.taskbar.paid" }
        free { applicationId "com.farmerbb.taskbar" }
    }

    sourceSets {
        main {
            java { srcDirs('src/main/java', 'src/playstore/java', "src/compat-${SDK_VERSION}/java") }
            res { srcDirs('src/main/res', 'src/playstore/res') }
            manifest.srcFile 'src/playstore/AndroidManifest.xml'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation 'com.mikepenz:iconics-core:3.0.4@aar'
    implementation 'com.mikepenz:foundation-icons-typeface:3.0.0.3@aar'
    implementation 'moe.banana:toast-compat:1.0.5'
    implementation group:'com.twofortyfouram', name:'android-plugin-api-for-locale', version:'[1.0.2,2.0['
}


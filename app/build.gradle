ext {
    SUPPORT_LIBRARY_VERSION = "27.0.0"
}

apply plugin: 'com.android.application'

repositories {
    google()

    maven {
        url "https://jitpack.io"
    }
}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.farmerbb.taskbar"
        minSdkVersion 21
        targetSdkVersion 27

        versionCode 171
        versionName "3.7.2"

        resConfigs "en", "ja", "ru", "de", "zh-rCN"

        buildConfigField "String", "BASE_APPLICATION_ID", "\"com.farmerbb.taskbar\""
        buildConfigField "String", "PAID_APPLICATION_ID", "\"com.farmerbb.taskbar.paid\""
        buildConfigField "String", "SUPPORT_APPLICATION_ID", "\"com.farmerbb.taskbar.support\""
        buildConfigField "String", "ANDROIDX86_APPLICATION_ID", "\"com.farmerbb.taskbar.androidx86\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            if(System.getenv("KSTOREFILE") != null)
                storeFile file(System.getenv("KSTOREFILE"))

            storePassword System.getenv("KSTOREPWD")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "@string/app_name_debug"]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]
        }
    }

    flavorDimensions "default"

    productFlavors {
        paid {
            dimension "default"
            applicationId "com.farmerbb.taskbar.paid"
        }

        free {
            dimension "default"
            applicationId "com.farmerbb.taskbar"
        }
    }

    sourceSets {
        main {
            java { srcDirs('src/main/java', 'src/playstore/java') }
            res { srcDirs('src/main/res', 'src/playstore/res') }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
    implementation 'com.mikepenz:iconics-core:2.9.3@aar'
    implementation 'com.mikepenz:foundation-icons-typeface:3.0.0.3@aar'
    implementation 'moe.banana:toast-compat:1.0.5'
}

